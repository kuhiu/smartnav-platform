#@TYPE: Machine
#@NAME: Generic k26-som
#@DESCRIPTION: Kria K26 SOM

#### Preamble
MACHINEOVERRIDES =. "${@['', 'k26-som:']['k26-som' != '${MACHINE}']}"
MACHINEOVERRIDES =. "k26:"
#### Regular settings follow

SOC_VARIANT ?= "ev"

require conf/machine/zynqmp-generic.conf

MACHINE_HWCODECS = "libomxil-xlnx"

#pmu-firmware variables
extra_settings:pn-pmu-firmware = ""
YAML_COMPILER_FLAGS:append:pn-pmu-firmware = " ${extra_settings}"
YAML_SERIAL_CONSOLE_STDIN:pn-pmu-firmware = "psu_uart_1"
YAML_SERIAL_CONSOLE_STDOUT:pn-pmu-firmware = "psu_uart_1"

#fsbl variables
YAML_SERIAL_CONSOLE_STDIN:pn-fsbl-firmware = "psu_uart_1"
YAML_SERIAL_CONSOLE_STDOUT:pn-fsbl-firmware = "psu_uart_1"
compilerflags:pn-fsbl-firmware = "-DFSBL_DEBUG"
YAML_COMPILER_FLAGS:append:pn-fsbl-firmware = " ${compilerflags}"

#FPGA manager Variables
MACHINE_FEATURES += "fpga-overlay"

#EXTRA_IMAGEDEPENDS:append = " virtual/bootloader virtual/fsbl virtual/pmu-firmware arm-trusted-firmware qemu-devicetrees u-boot-zynq-scr virtual/imgsel virtual/uboot-dtb"
EXTRA_IMAGEDEPENDS += "board-id-data"

SPL_BINARY = ""
EXTRA_IMAGEDEPENDS:remove = "virtual/boot-bin "
#PACKAGES_LIST:append = " imgsel"
YAML_DT_BOARD_FLAGS:k26-som = "{BOARD zynqmp-smk-k26-reva}"

YAML_SERIAL_CONSOLE_BAUDRATE = "115200"

SERIAL_CONSOLES = "115200;ttyPS1"

QB_OPT_APPEND = "\
    -nodefaults \
    -hw-dtb ${DEPLOY_DIR_IMAGE}/qemu-hw-devicetrees/multiarch/board-zynqmp-k26-starterkit-virt.dtb \
    ${@qemu_zynqmp_unhalt(d, True)} \
    -device loader,file=${DEPLOY_DIR_IMAGE}/arm-trusted-firmware.elf,cpu-num=0 \
    -device loader,file=${DEPLOY_DIR_IMAGE}/u-boot.elf \
    -device loader,file=${DEPLOY_DIR_IMAGE}/fit-dtb.blob,addr=0x100000,force-raw=on \
    ${@qemu_add_extra_args(d)} \
    -blockdev filename=${DEPLOY_DIR_IMAGE}/som-eeprom.bin,node-name=i2c1.eeprom-50,driver=file \
    -blockdev node-name=pmu-cfg,filename=${DEPLOY_DIR_IMAGE}/pmu-conf.bin,driver=file \
    -global xlnx,zynqmp-boot.drive=pmu-cfg \
    -pmu-args '${QB_PMU_OPT}' \
"

# runqemu assumes the first serial device is used but Kria SOM uses the second, the following line is
# required in order to mark the first serial as inactive as the runqemu serial config is appended
QB_OPT_APPEND += " -serial null"

#### No additional settings should be after the Postamble
#### Postamble
PACKAGE_EXTRA_ARCHS:append = "${@['', ' k26_som']['k26-som' != "${MACHINE}"]}"
