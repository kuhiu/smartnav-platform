require conf/distro/poky.conf
require conf/distro/include/petalinux-features.conf
require conf/distro/include/petalinux-mirrors.conf
require conf/distro/include/petalinux-version.conf
include conf/distro/include/xilinx-mirrors.conf
include conf/distro/include/petalinux-feed.conf

DISTRO = "petalinux"
DISTRO_NAME = "PetaLinux"

# Define the ROS2 Yocto release
ROS_OE_RELEASE_SERIES = "honister"

# Define ROS2 distro release
ROS_DISTRO = "humble"

DISTRO_VERSION := "${@ '_'.join("${XILINX_VER_MAIN} ${XILINX_VER_UPDATE} ${XILINX_VER_BUILD}".split())}"

SDK_VENDOR = "-petalinux"
SDK_VERSION := "${@'${XILINX_VER_MAIN}'.replace('snapshot-${DATE}','snapshot')}"

MAINTAINER = "Xilinx <meta-xilinx@yoctoproject.org>"

TARGET_VENDOR = "-xilinx"

SANITY_TESTED_DISTROS:append = " \
	ubuntu-18.04 \n \
	ubuntu-20.04 \n \
	centos-7.4 \n \
	centos-7.5 \n \
	centos-7.6 \n \
	centos-7.7 \n \
	centos-7.9 \n \
	centos-8 \n \
	centos-8.2 \n \
	centos-8.3 \n \
	centos-8.4 \n \
	centos-8.5 \n \
	centos-8.6 \n \
	rhel-7.4 \n \
	rhel-7.5 \n \
	rhel-7.6 \n \
	rhel-7.7 \n \
	rhel-7.9 \n \
	rhel-8.2 \n \
	rhel-8.3 \n \
	rhel-8.4 \n \
	rhel-8.5 \n \
	rhel-8.6 \n \
	sles-15.2 \n \
	"

# Blacklist packages
INHERIT += "blacklist"
PNBLACKLIST[distro-feed-configs] = "Not supported, see petalinux-image-common.inc for feed config."
PNBLACKLIST[qemu-native] = "Only qemu-xilinx-native is support."
PNBLACKLIST[nativesdk-qemu] = "Only nativesdk-qemu is supported."
PNBLACKLIST[qemu-helper-native] = "Only qemu-xilinx-helper-native is supported."

INHERIT += "buildhistory"
BUILDHISTORY_COMMIT = "1"

# Verify the user's configuration is valid
INHERIT += "plnx-externalsrc"

#INHERIT += "archiver"

# Enable the PR Service
PRSERV_HOST ?= "localhost:0"

COPY_LIC_MANIFEST = "1"
LICENSE_CREATE_PACKAGE = "1"
BB_GENERATE_MIRROR_TARBALLS = "1"

CONNECTIVITY_CHECK_URIS = ""

# Do not include clang in generated SDKs
CLANGSDK ?= "0"

# Make sure PR service is on
#PRSERV_HOST ??= "localhost:0"

PREFERRED_VERSION_xen = "4.16+git%"
PREFERRED_VERSION_xen-tools = "4.16+stable%"

# Required for vitis
PREFERRED_VERSION_glog ?= "0.4%"

# Configure default users/groups
# Default rules (assumed no debug-tweaks image feature):
# * disabled root login (set by system default)
# * Add a user 'petalinux' with no password
#   - SSH won't allow passwd-less login w/o debug tweaks, so requires console login
#   - Set to immediately expire
#   - Add to the sudoers file
# * Add 'petalinux' user to the audio group
# * Add 'petalinux' user to the video group
IMAGE_CLASSES += "extrausers"
EXTRA_USERS_PARAMS ?= "\
    useradd -p '' petalinux;passwd-expire petalinux; \
    usermod -a -G audio petalinux; \
    usermod -a -G video petalinux; \
    groupadd -r aie; \
    usermod -a -G aie petalinux; \
"
USERADDEXTENSION:append = " plnx-useradd-sudoers"
EXTRA_USERS_SUDOERS ?= "petalinux ALL=(ALL) ALL;"

